buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:5.2.1'
    }
}
 

plugins {
    id 'checkstyle'
    id 'java-library'
    id 'distribution'
    id 'maven-publish'
    id 'signing'
    id "com.github.johnrengelman.shadow" version "4.0.3"
    id "kr.motd.sphinx" version "2.3.1"
    id "jacoco"
    id "org.owasp.dependencycheck" version "5.2.1"

}

apply plugin: 'base'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'org.owasp.dependencycheck'



group 'com.opendxl'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencyCheck {
    suppressionFile = file("${projectDir}/config/dependency-check/owasp-suppression.xml")
}

jacoco {
    toolVersion = "0.8.6"
    reportsDir = file("$buildDir/reports/coverage")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("$buildDir/reports/coverage")
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.23.2'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    implementation 'com.e-movimento.tinytools:privilegedaccessor:1.2.2'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'commons-codec:commons-codec:1.15'
    implementation ('org.apache.httpcomponents:httpclient:4.5.13'){
        exclude group : 'common-codec', module: 'commons-codec'
    }
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.apache.logging.log4j:log4j-api:2.17.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.17.0'
}

// Jar Manifest info
jar {
    manifest {
        attributes 'Main-Class': 'com.opendxl.streaming.cli.CommandLineInterface',
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
}

// Task for creating a jar with the source files
task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// Task for creating a jar with JavaDoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    source = sourceSets.main.allJava
    options.memberLevel = JavadocMemberLevel.PUBLIC
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

// Set up the distribution zip
distributions {
    main {
        baseName = 'dxlstreamingclient-java-sdk'
        contents {
            from(jar) {
                into('lib')
            }
            from(shadowJar) {
                into("lib")
            }
            from(configurations.compileClasspath) {
                into("lib/3rdparty")
            }
            from(sphinx) {
                into('doc')
            }
            from('sample') {
                into 'sample'
            }
            from(shadowJar) {
                into("sample/lib")
            }
            from('distribution')
        }

    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/java'
        }
    }
    sample {
        java {
            srcDir 'sample/src'
        }
        compileClasspath += main.output + configurations.compileClasspath
        runtimeClasspath += main.output + configurations.runtimeClasspath
    }
    test {
        resources {
            srcDir 'src/test/resources'
        }
    }
}


// Make check style run before creating a jar
jar.dependsOn(checkstyleMain)

// Force distZip to be dependent on javadocJar and sourceJar
distZip.dependsOn javadocJar
distZip.dependsOn sourceJar

sphinx {
    // Change the source directory.
    sourceDirectory = "${projectDir}/docs"
    // Change the output directory.
    outputDirectory = "${project.buildDir}/docs"
}

task versionFile()  {
    doLast {
        new File("${projectDir}/VERSION").text = """$version"""
    }
}

checkstyle {
    sourceSets = []
    toolVersion '8.39'

}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            pom {
                name = 'OpenDXL Streaming Java Client'
                description = 'An OpenDXL streaming client library for Java'
                url = 'https://github.com/opendxl/opendxl-streaming-client-java'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        email = 'support@opendxl.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/opendxl/opendxl-streaming-client-java.git'
                    developerConnection = 'scm:git:ssh://github.com/opendxl/opendxl-streaming-client-java.git'
                    url = 'https://github.com/opendxl/opendxl-streaming-client-java'
                }
            }

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

        }
    }

    repositories {
        maven {
            url = "$buildDir/repo"

//            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
//            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
//            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
//            credentials {
//                username sonatypeUsername
//                password sonatypePassword
//            }
        }
    }

}

// Replace version string in some files that contains version reference
task replaceVersionInREADME() {
    if (!version.endsWith('SNAPSHOT')) {
        // Maven
        ant.replaceregexp(match: '<version>(.+)</version>', replace: "<version>${version}</version>", flags: 'g', byline: true) {
            fileset(dir: '.', includes: 'README.md')
        }
        ant.replaceregexp(match: '<version>(.+)</version>', replace: "<version>${version}</version>", flags: 'g', byline: true) {
            fileset(dir: 'docs', includes: 'index.rst')
        }
        // Gradle
        ant.replaceregexp(match: "com\\.opendxl\\.:dxlstreamingclient\\:(.+)", replace: "com.opendxl:dxlstreamingclient:${version}'", flags: 'g', byline: true) {
            fileset(dir: '.', includes: 'README.md')
        }
        ant.replaceregexp(match: "com\\.opendxl\\:dxlstreamingclient\\:(.+)", replace: "com.opendxl:dxlstreamingclient:${version}'", flags: 'g', byline: true) {
            fileset(dir: 'docs', includes: 'index.rst')
        }
    }
}

signing {sign publishing.publications.maven}
check.dependsOn dependencyCheckAnalyze
distZip.dependsOn javadocJar
distZip.dependsOn sourceJar
sphinx.dependsOn(versionFile)
assemble.dependsOn(replaceVersionInREADME)